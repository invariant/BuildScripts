#!/bin/zsh
set -eu; base=${0:a:h}/..; source $base/common; cd_info
source $base/post/post_common

# read in params
load_info uuid app_path app_zip product build version

# check status
echo "checking notarization status for uuid:" $uuid
json=$(altool --notarization-info $uuid)
echo $json | jq

# continue only when Apple has done the deed
[[ $(param $json notarization-info Status) == success ]] || { echo "not cooked yet"; exit 1 }

# staple it
echo "stapling ${app_path}"
xcrun stapler staple $app_path

# re-zip
echo "zipping stapled app to:" ${app_zip}
/usr/bin/ditto -c -k --keepParent $app_path $app_zip

# save the stapled zip to storage
storage_path=$(storage_path)
zip_dest=$storage_path/${app_zip:t}
print "saving to $zip_dest"
cp -f $app_zip $zip_dest

stapled='1'
save_info app_zip stapled