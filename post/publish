#!/bin/zsh
set -eu; base=${0:a:h}/..; source $base/common; cd_info

# load vars
load_info product app_zip version build channel edition slug min_system stapled verified 

# get zip file size
size=$(stat -f%z $app_zip); show size
save_info size

# make jekyll template for appcast
metafile=$(date +%F)-$slug.md; show metafile
show PWD

# build release file
cat <<eos > $metafile
---
layout: appcast
date: $(date +'%F %H:00:00 %z')
product: $product
channel: $channel
version: $build
filename: ${app_zip:t}
short_version_string: '$version'
size: $size
minimum_system_version: '$min_system'
published: no
---

Your notes here.

eos

print
cat $metafile
print 

# preserve existing file
meta_dest=$PILOTMOON_WWW_RELEASES_LOCATION/$metafile
if [[ -f $meta_dest ]]; then
  meta_backup=${meta_dest}.backup-$(uuid)
  print "* copying existing metafile to $meta_backup"
  mv $meta_dest $meta_backup
fi
# copy to destination
print "* copying metafile to $meta_dest"
cp $metafile $meta_dest

# copy zip to downloads location
downloads=$PILOTMOON_WWW_DOWNLOADS_LOCATION
if [[ $channel == $channel_beta ]]; then
  downloads=$downloads/beta
fi
print "* placing zip in $downloads"
cp -f $app_zip $downloads

published=1; save_info published

