#!/bin/zsh
set -eu; base=${0:a:h}/..; source $base/common; sayhi $0
show allowed_languages
show signing_identity

function strip_i386 {
  if lipo $1 -verify_arch i386; then
    lipo -remove i386 $1 -output $1 || true
  fi
}

# construct paths we will work with
sparkle_path=$1
sparkle_a=$sparkle_path/Versions/A
sparkle_resources=$sparkle_a/Resources
sparkle_bin=$sparkle_a/Sparkle
finish_app=$sparkle_resources/finish_installation.app
finish_app_resources=$finish_app/Contents/Resources
finish_app_bin=$finish_app/Contents/MacOS/finish_installation

# checks
show sparkle_path sparkle_resources finish_app finish_app_resources finish_app_bin
[[ -d $sparkle_path && -d $sparkle_resources && -x $sparkle_bin && -d $finish_app && -d $finish_app_resources && -x $finish_app_bin ]] || { print "bad paths"; exit 1 }

# process finish_installation.app
$base/build/remove_langs $finish_app_resources $allowed_languages # langs
rm -rf $finish_app_resources/Sparkle.icns # icon
strip_i386 $finish_app_bin # lipo
codesign --timestamp=none -o runtime -s $signing_identity -f $finish_app # sign

# process Sparkle.framework itself
$base/build/remove_langs $sparkle_resources $allowed_languages # langs
rm -rfv $sparkle_a/Headers
rm -fv $sparkle_path/Headers
strip_i386 $sparkle_bin # lipo
codesign --timestamp=none -o runtime -s $signing_identity -f $sparkle_a # sign

saybye $0
